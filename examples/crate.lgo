SCRIPT

to pensup
	penup
	toggle
	penup
	toggle
end

to pensdown 
	pendown
	toggle
	pendown
	toggle
end

to setpenscolor :color
	setpencolor :color
	toggle
	setpencolor :color
	toggle
end

to square :side :color
	repeat 4 [
		fd :side
		rt 90
	]
	make "s2 :side / 2
	
	pensup
	fd :s2
	rt 90
	fd :s2

	setpenscolor :color
	fill

	rt 180
	fd :s2
	lt 90
	fd :s2

	rt 180
	pensdown
end

to crate :s :num :startcol
	pensup
	fd :num * :s / 2
	rt 90
	fd :num * :s / 2
	rt 90
	pensdown
	
	make "c1 :startcol
	repeat :num [
		make "c1 :c1 + 1
		if :c1 = 26 [
			make "c1 1
		]
		make "c :c1
		repeat :num [
			square :s :c
			fd :s
			make "c :c + 1
			if :c = 26 [
				make "c 1
			]
		]
		rt 180
		fd :num * :s
		lt 90
		fd :s
		lt 90
	]
end

to deform :size
	make "x1 (-300) + random 600
	make "y1 (-300) + random 600
	make "x2 (-300) + random 600
	make "y2 (-300) + random 600
	make "x3 (-300) + random 600
	make "y3 (-300) + random 600

	threepointsmoebius :x1 :y1 :x2 :y2 :x3 :y3
		:x1 - :size + random (2 * :size + 1)
		:y1 - :size + random (2 * :size + 1)
		:x2 - :size + random (2 * :size + 1)
		:y2 - :size + random (2 * :size + 1)
		:x3 - :size + random (2 * :size + 1)
		:y3 - :size + random (2 * :size + 1)
end

to main 
	inverse 0 0 180
	crate 50 10 10
end
